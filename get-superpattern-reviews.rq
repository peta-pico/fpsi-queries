prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix dct: <http://purl.org/dc/terms/>
prefix np: <http://www.nanopub.org/nschema#>
prefix npx: <http://purl.org/nanopub/x/>
prefix npa: <http://purl.org/nanopub/admin/>
prefix frbr: <http://purl.org/vocab/frbr/core#>
prefix lf: <https://w3id.org/linkflows/reviews/>

prefix respond: <http://localhost:37373/publish?template=http://purl.org/np/RAe785M0p3cxpVXp2dhVdspVyj3YMdDdb0JEQdKPjO96E&param_OtherComment=http://purl.org/np/>

select distinct ?submitted_np ?authorname ?review_np ?reviewername ?aspect ?positivity ?action ?impact ?text where {
  graph ?submission_np_head {
    ?submission_np np:hasAssertion ?submission_np_assertion ;
      np:hasPublicationInfo ?submission_np_pubinfo ;
      a np:Nanopublication .
  }
  graph ?submission_np_assertion {
    ?submitted_np frbr:partOf <https://w3id.org/linkflows/formalization-papers/DataScienceSpecialIssue> .
  }
  graph ?submission_np_pubinfo {
    ?submission_np dct:creator ?__author_iri .
  }
  optional { ?__author_iri foaf:name ?authorname . }
  graph npa:graph {
    ?submission_np npa:hasValidSignatureForPublicKey ?pubkey .
  }

  filter not exists {
    graph npa:graph { ?newversion npa:hasHeadGraph ?nh . ?newversion npa:hasValidSignatureForPublicKey ?pubkey . }
    graph ?nh { ?newversion np:hasPublicationInfo ?ni . }
    graph ?ni { ?newversion npx:supersedes ?submission_np . }
  }
  filter not exists {
    graph npa:graph { ?retraction npa:hasHeadGraph ?rh . ?retraction npa:hasValidSignatureForPublicKey ?pubkey . }
    graph ?rh { ?retraction np:hasAssertion ?ra . }
    graph ?ra { ?somebody npx:retracts ?submission_np . }
  }

  graph ?review_np_head {
    ?review_np np:hasAssertion ?review_np_assertion ;
      np:hasPublicationInfo ?review_np_pubinfo ;
      a np:Nanopublication .
  }
  graph ?review_np_assertion {
    ?r a lf:ReviewComment .
    ?r lf:refersTo ?submitted_np .
    ?r a ?aspect .
    values ?aspect { lf:ContentComment lf:StyleComment lf:SyntaxComment }
    ?r a ?positivity .
    values ?positivity { lf:PositiveComment lf:NeutralComment lf:NegativeComment }
    ?r a ?action .
    values ?action { lf:ActionNeededComment lf:NoActionNeededComment lf:SuggestionComment }
    ?r lf:hasImpact ?impact .
    ?r lf:hasCommentText ?textpre .
  }
  bind(strdt(replace(?textpre, "[\u0001-\u0008\u000B\u000C\u000E-\u001F]", "[NON-PRINTABLE-CHARACTER]"), xsd:string) as ?text)
  graph ?review_np_pubinfo {
    ?review_np dct:creator ?__reviewer_iri .
  }
  optional { ?__reviewer_iri foaf:name ?reviewername . }
  graph npa:graph {
    ?review_np npa:hasValidSignatureForPublicKey ?reviewer_pubkey .
  }

  filter not exists {
    graph npa:graph { ?newversion npa:hasHeadGraph ?nh . ?newversion npa:hasValidSignatureForPublicKey ?reviewer_pubkey . }
    graph ?nh { ?newversion np:hasPublicationInfo ?ni . }
    graph ?ni { ?newversion npx:supersedes ?review_np . }
  }
  filter not exists {
    graph npa:graph { ?retraction npa:hasHeadGraph ?rh . ?retraction npa:hasValidSignatureForPublicKey ?reviewer_pubkey . }
    graph ?rh { ?retraction np:hasAssertion ?ra . }
    graph ?ra { ?somebody npx:retracts ?review_np . }
  }

} order by ?submitted_np ?review_np
