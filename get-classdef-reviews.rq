prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix dct: <http://purl.org/dc/terms/>
prefix np: <http://www.nanopub.org/nschema#>
prefix npx: <http://purl.org/nanopub/x/>
prefix npa: <http://purl.org/nanopub/admin/>
prefix vp: <http://purl.org/nanopub/admin/hasValidSignatureForPublicKey>
prefix frbr: <http://purl.org/vocab/frbr/core#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

prefix Syntax: <https://w3id.org/linkflows/reviews/SyntaxComment>
prefix Style: <https://w3id.org/linkflows/reviews/StyleComment>
prefix Content: <https://w3id.org/linkflows/reviews/ContentComment>

prefix Neg: <https://w3id.org/linkflows/reviews/NegativeComment>
prefix Neutr: <https://w3id.org/linkflows/reviews/NeutralComment>
prefix Pos: <https://w3id.org/linkflows/reviews/PositiveComment>

prefix NoActionNeeded: <https://w3id.org/linkflows/reviews/NoActionNeededComment>
prefix Suggestion: <https://w3id.org/linkflows/reviews/SuggestionComment>
prefix ActionNeeded: <https://w3id.org/linkflows/reviews/ActionNeededComment>

prefix Addressed: <https://w3id.org/linkflows/reviews/PointAddressedComment>
prefix PartiallyAddressed: <https://w3id.org/linkflows/reviews/PointPartiallyAddressedComment>
prefix NotAddressed: <https://w3id.org/linkflows/reviews/PointNotAddressedComment>

prefix Agreed: <https://w3id.org/linkflows/reviews/AgreementComment>
prefix PartialAgreed: <https://w3id.org/linkflows/reviews/PartialAgreementComment>
prefix Disagreed: <https://w3id.org/linkflows/reviews/DisagreementComment>

prefix lf: <https://w3id.org/linkflows/reviews/>

prefix respond: <http://localhost:37373/publish?template=http://purl.org/np/RAe785M0p3cxpVXp2dhVdspVyj3YMdDdb0JEQdKPjO96E&param_OtherComment=>

select distinct ?class_np ?class_npLabel ?class_name ?author ?authorLabel ?review_np ?review_npLabel ?reviewer ?reviewerLabel ?aspect ?pos ?action ?imp ?mentioning ?mentioningLabel ?text ?resp_np ?resp_npLabel ?resp_text ?agreement ?addressed ?add_resp ?add_respLabel where {
  graph ?submission_np_head {
    ?submission_np np:hasAssertion ?submission_np_assertion ;
      np:hasPublicationInfo ?submission_np_pubinfo .
  }
  graph ?submission_np_assertion {
    ?submitted_np frbr:partOf <https://w3id.org/linkflows/formalization-papers/DataScienceSpecialIssue> .
  }
  graph ?submission_np_pubinfo {
    ?submission_np dct:creator ?__author_iri .
    bind(?__author_iri as ?author)
  }
  optional { ?__author_iri foaf:name ?authorLabel . }
  filter( ?authorLabel != "Cristina-Iulia Bucu"^^xsd:string )
  graph npa:graph {
    ?submission_np vp: ?pubkey .
  }

  filter not exists {
    graph npa:graph { ?retraction vp: ?pubkey . } ?retraction np:hasAssertion ?sa .  # TODO: move into npa:graph
    graph ?sa { ?somebody npx:retracts ?submission_np . }
  }

  graph ?submitted_np_head {
    ?submitted_np np:hasAssertion ?submitted_np_assertion .
  }
  graph ?submitted_np_assertion {
    ?submitted_np_s ?submitted_np_p ?class .
  }

  graph ?sp_np_head {
    ?sp_np np:hasAssertion ?sp_np_assertion .
  }
  graph ?sp_np_assertion {
    ?spi ?p ?class .
  }
  graph npa:graph {
    ?sp_np vp: ?pubkey .
  }

  graph ?class_np_head {
    ?class_np np:hasAssertion ?class_np_assertion ;
      np:hasPublicationInfo ?class_np_pubinfo .
    bind(replace(str(?class_np), '^.*(RA[a-zA-Z0-9-_]{4})[a-zA-Z0-9-_]{39}$', '$1') as ?class_npLabel)
  }
  graph ?class_np_assertion {
    ?class rdfs:label ?class_name .
  }
  graph ?class_np_pubinfo {
    ?class_np npx:introduces ?class .
  }
  graph npa:graph {
    ?class_np vp: ?pubkey .
  }

  graph ?review_np_head {
    ?review_np np:hasAssertion ?review_np_assertion ;
      np:hasPublicationInfo ?review_np_pubinfo ;
      a np:Nanopublication .
    bind(replace(str(?review_np), '^.*(RA[a-zA-Z0-9-_]{4})[a-zA-Z0-9-_]{39}$', '$1') as ?review_npLabel)
  }
  graph ?review_np_assertion {
    ?r a lf:ReviewComment .
    ?r lf:refersTo ?class_np .
    optional {
      ?r lf:refersToMentioningOf ?mentioning .
      bind(replace(replace(str(?mentioning), '^.*[/#]([^/#]+)[/#]?$', '$1'), '^(RA[a-zA-Z0-9-_]{8})[a-zA-Z0-9-_]{35}$', '$1') as ?mentioningLabel)
    }
    ?r a ?aspect .
    values ?aspect { lf:ContentComment lf:StyleComment lf:SyntaxComment }
    ?r a ?pos .
    values ?pos { lf:PositiveComment lf:NeutralComment lf:NegativeComment }
    ?r a ?action .
    values ?action { lf:ActionNeededComment lf:NoActionNeededComment lf:SuggestionComment }
    ?r lf:hasImpact ?imp .
    ?r lf:hasCommentText ?textpre .
  }
  bind(strdt(replace(?textpre, "[^\n\t\r -􏿿]", "[NON-PRINTABLE-CHARACTER]"), xsd:string) as ?text)
  graph ?review_np_pubinfo {
    ?review_np dct:creator ?__reviewer_iri .
    bind(?__reviewer_iri as ?reviewer)
  }
  optional { ?__reviewer_iri foaf:name ?reviewerLabel . }
  filter( ?reviewerLabel != "Cristina-Iulia Bucu"^^xsd:string )
  graph npa:graph {
    ?review_np vp: ?reviewer_pubkey .
  }

  filter not exists {
    graph npa:graph { ?newversion vp: ?reviewer_pubkey . } ?newversion np:hasPublicationInfo ?ri .  # TODO: move into npa:graph
    graph ?ri { ?newversion npx:supersedes ?review_np . }
  }
  filter not exists {
    graph npa:graph { ?retraction vp: ?reviewer_pubkey . } ?retraction np:hasAssertion ?ra .  # TODO: move into npa:graph
    graph ?ra { ?somebody npx:retracts ?review_np . }
  }

  optional {
    graph npa:graph { ?update_np vp: ?pubkey . } ?update_np np:hasPublicationInfo ?xi .  # TODO: move into npa:graph
    graph ?xi { ?update_np lf:isUpdateOf ?class_np . }
    bind(replace(str(?update_np), '^.*(RA[a-zA-Z0-9-_]{4})[a-zA-Z0-9-_]{39}$', '$1') as ?update_npLabel)
  }

  optional {
    graph npa:graph { ?resp_np vp: ?pubkey . } ?resp_np np:hasAssertion ?respa .
    graph ?respa {
      ?resp_comment lf:isResponseTo ?review_np ;
        lf:hasCommentText ?resp_textpre .
      bind(strdt(replace(?resp_textpre, "[^\n\t\r -􏿿]", "[NON-PRINTABLE-CHARACTER]"), xsd:string) as ?resp_text)
      ?resp_comment a ?agreement .
      values ?agreement { lf:AgreementComment lf:DisagreementComment lf:PartialAgreementComment }
      ?resp_comment a ?addressed .
      values ?addressed { lf:PointAddressedComment lf:PointNotAddressedComment lf:PointPartiallyAddressedComment }
    }
    bind(replace(str(?resp_np), '^.*(RA[a-zA-Z0-9-_]{4})[a-zA-Z0-9-_]{39}$', '$1') as ?resp_npLabel)
    filter not exists {
      graph npa:graph { ?retraction vp: ?pubkey . } ?retraction np:hasAssertion ?respra .  # TODO: move into npa:graph
      graph ?respra { ?somebody npx:retracts ?resp_np . }
    }
  }

  bind(if(bound(?resp_np), "", uri(concat(respond:, ?review_np, "&param_ReferredObject=", coalesce(?update_np,?class_np)))) as ?add_resp)
  bind(if(bound(?update_np), if(bound(?resp_np), "", concat(coalesce(?authorLabel, "the author"), ' can click here to respond')), concat(coalesce(?authorLabel, "the author"), ' can click here to respond if no update is needed (otherwise update class nanopub first)')) as ?add_respLabel)

} order by ?submitted_np ?review_np
