prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix dct: <http://purl.org/dc/terms/>
prefix np: <http://www.nanopub.org/nschema#>
prefix npx: <http://purl.org/nanopub/x/>
prefix npa: <http://purl.org/nanopub/admin/>
prefix sp: <https://w3id.org/linkflows/superpattern/terms/>

select distinct ?sp_np ?author ?authorname ?date where {
  graph ?sp_np_head {
    ?sp_np np:hasAssertion ?sp_np_assertion ;
      np:hasPublicationInfo ?sp_np_pubinfo ;
      a np:Nanopublication .
  }
  graph ?sp_np_assertion {
    ?spi a sp:SuperPatternInstance .
  }
  graph ?sp_np_pubinfo {
    ?sp_np dct:creator ?author .
    ?sp_np dct:created ?date .
  }
  graph npa:graph {
    ?sp_np npa:hasValidSignatureForPublicKey ?pubkey .
  }
  optional { ?author foaf:name ?authorname . }
  filter(?author != <https://orcid.org/0000-0002-1267-0234>)
  filter(?author != <https://orcid.org/0000-0002-7114-6459>)

  filter not exists {
    graph npa:graph { ?newversion npa:hasHeadGraph ?nh . ?newversion npa:hasValidSignatureForPublicKey ?pubkey . }
    graph ?nh { ?newversion np:hasPublicationInfo ?ni . }
    graph ?ni { ?newversion npx:supersedes ?sp_np . }
  }
  filter not exists {
    graph npa:graph { ?retraction npa:hasHeadGraph ?rh . ?retraction npa:hasValidSignatureForPublicKey ?pubkey . }
    graph ?rh { ?retraction np:hasAssertion ?ra . }
    graph ?ra { ?somebody npx:retracts ?sp_np . }
  }

  bind(uri(concat("http://localhost:37373/publish?template=http://purl.org/np/RAeGHhPpt1GLzb_tInb87Wdnvep19Efieot9BBGoN_3TM&param_ReferredObject=", ?submitted_np)) as ?click_to_add_review)
} order by desc(?date)
