prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix dct: <http://purl.org/dc/terms/>
prefix np: <http://www.nanopub.org/nschema#>
prefix npx: <http://purl.org/nanopub/x/>
prefix npa: <http://purl.org/nanopub/admin/>
prefix frbr: <http://purl.org/vocab/frbr/core#>
prefix lf: <https://w3id.org/linkflows/reviews/>

prefix addreview: <http://localhost:37373/publish?template=http://purl.org/np/RAmIPyDSP2ZrKM3PaAjSOIhkbZJ0CV6lRkh3Mlv7AzFiw&param_ReferredObject=>
prefix addupdate: <http://localhost:37373/publish?template=http://purl.org/np/RAdpgRpigXtt8iPV9uOPf3wIT3qzOI8Sg2Q72CNV8g-Yo&pitemplate1=http://purl.org/np/RAOGu9Lh0BD4tbIRB9RG6RGRA_ObDh75NTbIqaWgxxs8M&piparam1_np=>

select distinct ?class_np ?class_npLabel ?class_name ?author ?authorLabel ?add_review ?add_reviewLabel ?update_np ?update_npLabel ?add_update ?add_updateLabel where {
  graph ?submission_np_head {
    ?submission_np np:hasAssertion ?submission_np_assertion ;
      np:hasPublicationInfo ?submission_np_pubinfo .
  }
  graph ?submission_np_assertion {
    ?submitted_np frbr:partOf <https://w3id.org/linkflows/formalization-papers/DataScienceSpecialIssue> .
  }
  graph ?submission_np_pubinfo {
    ?submission_np dct:creator ?__author_iri .
    bind(?__author_iri as ?author)
  }
  graph npa:graph {
    ?submission_np npa:hasValidSignatureForPublicKey ?pubkey .
  }
  optional { ?__author_iri foaf:name ?authorLabel . }

  graph ?submitted_np_head {
    ?submitted_np np:hasAssertion ?submitted_np_assertion .
  }
  graph ?submitted_np_assertion {
    ?submitted_np_s ?submitted_np_p ?class .
  }

  filter not exists {
    graph npa:graph { ?newversion npa:hasHeadGraph ?nh . ?newversion npa:hasValidSignatureForPublicKey ?pubkey . }
    graph ?nh { ?newversion np:hasPublicationInfo ?ni . }
    graph ?ni { ?newversion npx:supersedes ?submission_np . }
  }
  filter not exists {
    graph npa:graph { ?retraction npa:hasHeadGraph ?rh . ?retraction npa:hasValidSignatureForPublicKey ?pubkey . }
    graph ?rh { ?retraction np:hasAssertion ?ra . }
    graph ?ra { ?somebody npx:retracts ?submission_np . }
  }

  graph ?sp_np_head {
    ?sp_np np:hasAssertion ?sp_np_assertion .
  }
  graph ?sp_np_assertion {
    ?spi ?p ?class .
  }
  graph npa:graph {
    ?sp_np npa:hasValidSignatureForPublicKey ?pubkey .
  }

  graph ?class_np_head {
    ?class_np np:hasAssertion ?class_np_assertion ;
      np:hasPublicationInfo ?class_np_pubinfo .
    bind(replace(str(?class_np), '^.*(RA[a-zA-Z0-9-_]{8})[a-zA-Z0-9-_]{35}$', '$1') as ?class_npLabel)
  }
  graph ?class_np_assertion {
    ?class rdfs:label ?class_name .
  }
  graph ?class_np_pubinfo {
    ?class_np npx:introduces ?class .
  }
  graph npa:graph {
    ?class_np npa:hasValidSignatureForPublicKey ?pubkey .
  }

  bind(uri(concat(addreview:, ?class_np)) as ?add_review)
  bind('click here to add review' as ?add_reviewLabel)

  optional {
    graph npa:graph { ?update_np npa:hasHeadGraph ?sh . ?update_np npa:hasValidSignatureForPublicKey ?pubkey . }
    graph ?sh { ?update_np np:hasPublicationInfo ?si . }
    graph ?si { ?update_np lf:isUpdateOf ?class_np . }
    bind(replace(str(?update_np), '^.*(RA[a-zA-Z0-9-_]{8})[a-zA-Z0-9-_]{35}$', '$1') as ?update_npLabel)
  }

  bind(uri(concat(addupdate:, ?class_np, "&supersede=", ?class_np)) as ?add_update)
  bind(concat(coalesce(?authorLabel, "the author"), ' can click here to update in response to the reviews') as ?add_updateLabel)

} order by ?authorLabel
