prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix dct: <http://purl.org/dc/terms/>
prefix np: <http://www.nanopub.org/nschema#>
prefix npx: <http://purl.org/nanopub/x/>
prefix npa: <http://purl.org/nanopub/admin/>
prefix frbr: <http://purl.org/vocab/frbr/core#>
prefix lf: <https://w3id.org/linkflows/reviews/>

prefix addreview: <http://localhost:37373/publish?template=http://purl.org/np/RAmIPyDSP2ZrKM3PaAjSOIhkbZJ0CV6lRkh3Mlv7AzFiw&param_ReferredObject=>
prefix addupdate: <http://localhost:37373/publish?template=http://purl.org/np/RAv68imZrEjfcp2rnEg1hzoBqEVc0cQMtp9_1Za0BxNM4&pitemplate1=http://purl.org/np/RAOGu9Lh0BD4tbIRB9RG6RGRA_ObDh75NTbIqaWgxxs8M&piparam1_np=>

select distinct ?submitted_np ?submitted_npLabel ?is_update_of_np ?is_update_of_npLabel ?author ?authorLabel ?add_review ?add_reviewLabel ?add_update ?add_updateLabel where {
  graph ?submission_np_head {
    ?submission_np np:hasAssertion ?submission_np_assertion ;
      np:hasPublicationInfo ?submission_np_pubinfo ;
      a np:Nanopublication .
  }
  graph ?submission_np_assertion {
    ?submitted_np frbr:partOf <https://w3id.org/linkflows/formalization-papers/DataScienceSpecialIssue> .
    bind(replace(str(?submitted_np), '^.*(RA[a-zA-Z0-9-_]{8})[a-zA-Z0-9-_]{35}$', '$1') as ?submitted_npLabel)
  }
  graph ?submission_np_pubinfo {
    ?submission_np dct:creator ?__author_iri .
    optional {
      ?submission_np lf:isUpdateOf ?is_update_of_np .
      bind(replace(str(?is_update_of_np), '^.*(RA[a-zA-Z0-9-_]{8})[a-zA-Z0-9-_]{35}$', '$1') as ?is_update_of_npLabel)
    }
    bind(?__author_iri as ?author)
  }
  graph npa:graph {
    ?submission_np npa:hasValidSignatureForPublicKey ?pubkey .
  }
  optional { ?__author_iri foaf:name ?authorLabel . }

  filter not exists {
    graph npa:graph { ?retraction npa:hasHeadGraph ?rh . ?retraction npa:hasValidSignatureForPublicKey ?pubkey . }
    graph ?rh { ?retraction np:hasAssertion ?ra . }
    graph ?ra { ?somebody npx:retracts ?submitted_np . }
  }

  filter not exists {
    graph npa:graph { ?newversion npa:hasHeadGraph ?nh . ?newversion npa:hasValidSignatureForPublicKey ?pubkey . }
    graph ?nh { ?newversion np:hasPublicationInfo ?ni . }
    graph ?ni { ?newversion npx:supersedes ?submission_np . }
  }
  filter not exists {
    graph npa:graph { ?retraction npa:hasHeadGraph ?rh . ?retraction npa:hasValidSignatureForPublicKey ?pubkey . }
    graph ?rh { ?retraction np:hasAssertion ?ra . }
    graph ?ra { ?somebody npx:retracts ?submission_np . }
  }

  bind(uri(concat(addreview:, ?submitted_np)) as ?add_review)
  bind('click here to add review' as ?add_reviewLabel)

  bind(uri(concat(addupdate:, ?submitted_np)) as ?add_update)
  bind(concat(coalesce(?authorLabel, "the author"), ' can click here to update the submission in response to the reviews') as ?add_updateLabel)
} order by ?authorLabel
