prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix dct: <http://purl.org/dc/terms/>
prefix np: <http://www.nanopub.org/nschema#>
prefix npx: <http://purl.org/nanopub/x/>
prefix npa: <http://purl.org/nanopub/admin/>
prefix frbr: <http://purl.org/vocab/frbr/core#>
prefix lf: <https://w3id.org/linkflows/reviews/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

prefix addreview: <http://localhost:37373/publish?template=http://purl.org/np/RAmIPyDSP2ZrKM3PaAjSOIhkbZJ0CV6lRkh3Mlv7AzFiw&param_ReferredObject=>
prefix addupdate: <http://localhost:37373/publish?template=http://purl.org/np/RAv68imZrEjfcp2rnEg1hzoBqEVc0cQMtp9_1Za0BxNM4&pitemplate1=http://purl.org/np/RAOGu9Lh0BD4tbIRB9RG6RGRA_ObDh75NTbIqaWgxxs8M&piparam1_np=>

select distinct ?submitted_np ?submitted_npLabel ?author ?authorLabel ?add_review ?add_reviewLabel ?update_np ?update_npLabel ?add_update ?add_updateLabel where {
  
  graph npa:graph { ?submission_np npa:hasValidSignatureForPublicKey ?pubkey . }
    ?submission_np np:hasAssertion ?submission_np_a ; np:hasPublicationInfo ?submission_np_i .  # TODO: move into npa:graph
  graph ?submission_np_a {
    ?submitted_np frbr:partOf <https://w3id.org/linkflows/formalization-papers/DataScienceSpecialIssue> .
    bind(replace(str(?submitted_np), '^.*(RA[a-zA-Z0-9-_]{8})[a-zA-Z0-9-_]{35}$', '$1') as ?submitted_npLabel)
  }
  graph ?submission_np_i {
    ?submission_np dct:creator ?__author_iri .
    bind(?__author_iri as ?author)
  }
  filter not exists {
    graph npa:graph { ?retraction npa:hasValidSignatureForPublicKey ?pubkey . } ?retraction np:hasAssertion ?sra .  # TODO: move into npa:graph
    graph ?sra { ?somebody npx:retracts ?submission_np . }
  }

  optional { ?__author_iri foaf:name ?authorLabel . }
  filter( ?authorLabel != "Cristina-Iulia Bucu"^^xsd:string )

  filter not exists {
    graph npa:graph { ?retraction npa:hasValidSignatureForPublicKey ?pubkey . } ?retraction np:hasAssertion ?dra .  # TODO: move into npa:graph
    graph ?dra { ?somebody npx:retracts ?submitted_np . }
  }

  bind(uri(concat(addreview:, ?submitted_np)) as ?add_review)
  bind('click here to add review' as ?add_reviewLabel)

  optional {
    graph npa:graph { ?update_np npa:hasValidSignatureForPublicKey ?pubkey2 . } ?update_np np:hasPublicationInfo ?ui .  # TODO: move into npa:graph; TODO: check pubkey
    graph ?ui { ?update_np lf:isUpdateOf ?submitted_np . }
    bind(replace(str(?update_np), '^.*(RA[a-zA-Z0-9-_]{8})[a-zA-Z0-9-_]{35}$', '$1') as ?update_npLabel)
    filter not exists {
      graph npa:graph { ?retraction npa:hasValidSignatureForPublicKey ?pubkey2 . } ?retraction np:hasAssertion ?ura .  # TODO: move into npa:graph
      graph ?ura { ?somebody npx:retracts ?update_np . }
    }
  }

  bind(if(bound(?update_np), "", uri(concat(addupdate:, ?submitted_np, "&fill-all=", ?submitted_np))) as ?add_update)
  bind(if(bound(?update_np), "", concat(coalesce(?authorLabel, "the author"), ' can click here to update in response to the reviews')) as ?add_updateLabel)

} order by ?authorLabel
